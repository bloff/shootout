/* ------------------------------------------------------------------ */
/* The Great Computer Language Shootout                               */
/* http://shootout.alioth.debian.org/                                 */
/*                                                                    */
/* % regina -v                                                        */
/* REXX-Regina_3.3(MT) 5.00 25 Apr 2004                               */
/* % uname -orvmp                                                     */
/* 2.6.5-1.358 #1 Wed Oct 13 17:49:34 EST 2004 i686 i686 GNU/Linux    */
/*                                                                    */
/* Contributed by Anthony Borla                                       */
/* ------------------------------------------------------------------ */

width = ARG(1) ; if DATATYPE(width) \= 'NUM' | width < 1 then ; width = 600
height = width

limit = 2 ; limit_sqr = limit * limit ; iter = 50 ; byte_acc = 0 ; bit_num = 0

call LINEOUT , "P4" ; call LINEOUT , width height

do y = 0 to height - 1

  do x = 0 to width - 1

    Zr = 0 ; Zi = 0 
    Cr = (2 * x / width - 1.5) ; Ci = (2 * y / height - 1)

    do iter
      Tr = Zr * Zr - Zi * Zi + Cr
      Ti = 2 * Zr * Zi + Ci
      Zr = Tr ; Zi = Ti
      if (Zr * Zr + Zi * Zi) > limit_sqr then ; leave
    end

    byte_acc = byte_acc * 2
    if (Zr * Zr + Zi * Zi) <= limit_sqr then ; byte_acc = byte_acc + 1

    bit_num = bit_num + 1

    if bit_num == 8 then do
      call CHAROUT , D2C(byte_acc)
      byte_acc = 0 ; bit_num = 0
    end ; else ; if x == width - 1 then do
      byte_acc = byte_acc * 2 ** (8 - width // 8)
      call CHAROUT , D2C(byte_acc)
      byte_acc = 0 ; bit_num = 0
    end

  end

end

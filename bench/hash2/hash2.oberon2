(* The Great Computer Language Shootout
   http://shootout.alioth.debian.org

   contributed by Isaac Gouy (Oberon-2 novice)
*)

MODULE Hash2;
IMPORT LangShootout, SimpleHash, Out;

CONST
   BufferSize = 16;

VAR
   buffer: ARRAY BufferSize OF CHAR;
   n, i: LONGINT;
   table1, table2: SimpleHash.Hashtable;
   item1, item2: SimpleHash.Item;
   key, key1, key9999: SimpleHash.ItemKey;
   Digits: ARRAY 16 OF CHAR;    


PROCEDURE IntToString(x, r: LONGINT): SimpleHash.ItemKey;
VAR
   s: SimpleHash.ItemKey;
   i, j: LONGINT;
BEGIN
   i := 0;
   REPEAT buffer[i] := Digits[x MOD r]; x := x DIV r; INC(i); UNTIL x = 0;
   NEW(s, i);
   j := 0;
   REPEAT DEC(i); s[j] := buffer[i]; INC(j); UNTIL i = 0;
   RETURN s;
END IntToString;

PROCEDURE InitDigits();
BEGIN
   Digits[0] := "0"; Digits[1] := "1"; Digits[2] := "2"; Digits[3] := "3";
   Digits[4] := "4"; Digits[5] := "5"; Digits[6] := "6"; Digits[7] := "7";
   Digits[8] := "8"; Digits[9] := "9"; Digits[10] := "a"; Digits[11] := "b";      
   Digits[12] := "c"; Digits[13] := "d"; Digits[14] := "e"; Digits[15] := "f"; 
END InitDigits;


BEGIN
   InitDigits(); 
   n := LangShootout.Argi();
   table1 := SimpleHash.New(10000);
   table2 := SimpleHash.New(10000);

   FOR i := 0 TO 9999 DO
      key := IntToString(i,10);
      item1 := SimpleHash.AddKey(table1, key);
      item1.value := i;
   END;

   FOR i := 0 TO n-1 DO
      item1 := SimpleHash.First(table1);
      WHILE item1 # NIL DO
         item2 := SimpleHash.AddKey(table2, item1.key);
	 INC(item2.value, item1.value);
	 item1 := SimpleHash.Next(table1);
      END;
   END;

   key1 := IntToString(1,10);
   key9999 := IntToString(9999,10);

   item1 := SimpleHash.Find(table1, key1);
   Out.Int(item1.value, 1); Out.Char(20X);
   item1 := SimpleHash.Find(table1, key9999);
   Out.Int(item1.value, 0); Out.Char(20X);
   item2 := SimpleHash.Find(table2, key1);
   Out.Int(item2.value, 0); Out.Char(20X);
   item2 := SimpleHash.Find(table2, key9999);
   Out.Int(item2.value, 0); Out.Ln;
END Hash2.

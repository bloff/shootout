;;; The Great Computer Language Shootout
;;; http://shootout.alioth.debian.org/
;;; contributed by Dima Dorfman, 2005

(defun nsieve (m)
  (declare (type integer m))
  (let ((a (make-array (list m) :initial-element t
		       :element-type 'boolean)))
    (flet ((clear (i)
	     (loop for j from (+ i i) to (1- m) by i
		   do (setf (aref a j) nil))))
      (loop for i from 2 to (1- m)
	    when (aref a i) do (clear i)
	    count (aref a i)))))

(defun test (n)
  (let ((m (* 10000 (expt 2 n))))
    (format t "Primes up to~T~8<~d~>~T~8<~d~>~%" m (nsieve m))))

(defun main ()
  (let* ((args #+sbcl sb-ext:*posix-argv*
	       #+cmu extensions:*command-line-strings*
	       #+gcl si::*command-args*)
	 (n (parse-integer (car (last args)))))
    (when (>= n 0) (test n))
    (when (>= n 1) (test (- n 1)))
    (when (>= n 2) (test (- n 2)))))

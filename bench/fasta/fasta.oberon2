(* The Great Computer Language Shootout
   http://shootout.alioth.debian.org

   contributed by Isaac Gouy (Oberon-2 novice)
*)


MODULE Fasta;
IMPORT LangShootout, Random, Out;

CONST length = 60;

TYPE
   Frequency = RECORD
      c: CHAR;
      p: LONGREAL;
   END;

   Freqency4 = ARRAY 4 OF Frequency;
   Freqency17 = ARRAY 17 OF Frequency;

VAR
   dna1: Freqency4;
   dna2: Freqency17;
   n: LONGINT;


PROCEDURE MakeCumulative(VAR a: ARRAY OF Frequency);
VAR
   i: LONGINT;
   cp: LONGREAL;
BEGIN
   cp := 0.0;
   FOR i := 0 TO LEN(a) - 1 DO
      cp := cp + a[i].p;
      a[i].p := cp;
   END;
END MakeCumulative;


PROCEDURE MakeDna1(VAR a: Freqency4);
BEGIN
   a[0].c := "a";   a[0].p := 0.3029549426680;
   a[1].c := "c";   a[1].p := 0.1979883004921;
   a[2].c := "g";   a[2].p := 0.1975473066391;
   a[3].c := "t";   a[3].p := 0.3015094502008;

   MakeCumulative(a);
END MakeDna1;


PROCEDURE MakeDna2(VAR a: Freqency17);
BEGIN
   a[0].c := "a";   a[0].p := 0.250;
   a[1].c := "c";   a[1].p := 0.125;
   a[2].c := "g";   a[2].p := 0.125;
   a[3].c := "t";   a[3].p := 0.250;

   a[4].c := "U";   a[4].p := 0.019230769231;
   a[5].c := "R";   a[5].p := 0.019230769231;
   a[6].c := "Y";   a[6].p := 0.019230769231;
   a[7].c := "K";   a[7].p := 0.019230769231;
   a[8].c := "M";   a[8].p := 0.019230769231;
   a[9].c := "S";   a[9].p := 0.019230769231;
   a[10].c := "W";  a[10].p := 0.019230769231;
   a[11].c := "B";  a[11].p := 0.019230769231;
   a[12].c := "D";  a[12].p := 0.019230769231;
   a[13].c := "H";  a[13].p := 0.019230769231;
   a[14].c := "V";  a[14].p := 0.019230769231;
   a[15].c := "N";  a[15].p := 0.019230769231;
   a[16].c := "-";  a[16].p := 0.019230769231;

   MakeCumulative(a);
END MakeDna2;


PROCEDURE SelectRandom(VAR a: ARRAY OF Frequency): CHAR;
VAR
   i: LONGINT;
   r: LONGREAL;
BEGIN
   r := Random.Next(1.0);  
   FOR i := 0 TO LEN(a) - 1 DO
      IF r < a[i].p THEN RETURN a[i].c; END;
   END;
END SelectRandom;


PROCEDURE MakeFastaFragment(id,desc: ARRAY OF CHAR;
                            VAR a: ARRAY OF Frequency; 
                            n: LONGINT);
VAR
   i, m: LONGINT;
BEGIN
   Out.String(">"); Out.String(id); Out.Char(20X); Out.String(desc); Out.Ln;
   WHILE n > 0 DO
      IF n < length THEN m := n; ELSE m := length; END;
      FOR i := 0 TO m-1 DO Out.Char( SelectRandom(a) ); END; Out.Ln;
      DEC(n,length);
   END;
END MakeFastaFragment;


BEGIN 
   n := LangShootout.Argi();
   IF n = 1 THEN n := 1000; END;

   MakeDna1(dna1);
   MakeDna2(dna2);

   MakeFastaFragment('TestOne','test fragment',dna2,n*1);
   MakeFastaFragment('TestTwo','test fragment',dna1,n*2);
   MakeFastaFragment('TestThree','test fragment',dna2,n*3);
   MakeFastaFragment('TestFour','test fragment',dna1,n*4);
   MakeFastaFragment('TestFive','test fragment',dna2,n*5);
END Fasta.

#!/usr/bin/mzscheme -r
;; $Id: fannkuch.mzscheme,v 1.2 2005-04-24 17:12:36 bfulgham Exp $
;; fannkuch benchmark for The Computer Language Shootout
;; Written by Dima Dorfman, 2004
;;
;; Ever-so-slightly tweaked for MzScheme by Brent Fulgham

(define (vector-swap! v i j)
  (let ((t (vector-ref v i)))
    (vector-set! v i (vector-ref v j))
    (vector-set! v j t)))

(define (vector-reverse-slice! v i j)
  (do ((i i (add1 i))
       (j j (sub1 j)))
      ((<= (- j i) 1))
    (vector-swap! v i (sub1 j))))

(define (count-flips pi)
  (do ((rho (vector-copy pi))
       (i 0 (add1 i)))
      ((= (vector-ref rho 0) 1) i)
    (vector-reverse-slice! rho 0 (vector-ref rho 0))))

(define (vector-copy source)
  (do ((vec (make-vector (vector-length source)))
       (i 0 (add1 i)))
      ((= i (vector-length source)) vec)
    (vector-set! vec i (vector-ref source i))))

(define (successor! pi)
  (let ((i (let loop ((i (- (vector-length pi) 2)))
	     (cond ((< i 0) #f)
		   ((> (vector-ref pi (add1 i)) (vector-ref pi i)) i)
		   (else (loop (sub1 i)))))))
    (if (not i) #f
	(let* ((ith (vector-ref pi i))
	       (j (do ((j (sub1 (vector-length pi)) (sub1 j)))
		      ((> (vector-ref pi j) ith) j))))
	  (vector-swap! pi i j)
	  (vector-reverse-slice! pi (add1 i) (vector-length pi))
	  #t))))

(define (fannkuch n)
  (let ((pi (do ((pi (make-vector n))
		 (i 0 (add1 i)))
		((= i (vector-length pi)) pi)
	      (vector-set! pi i (add1 i)))))
    (let loop ((flips 0))
      (let ((flips (max (count-flips pi) flips)))
	(if (successor! pi)
	    (loop flips)
	    flips)))))

(define (main args)
  (if (< (vector-length args) 1)
      (begin (display "An argument is required") (newline) 2)
      (let ((n (string->number (vector-ref args 0))))
	(if (not n)
	    (begin (display "An integer is required") (newline) 2)
	    (let ((f (fannkuch n)))
	      (begin
		(display "Pfannkuchen(")
		(display n)
		(display ") = ")
		(display f)
		(newline))
	      0)))))

(main (current-command-line-arguments))

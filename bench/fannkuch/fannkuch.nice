/* The Great Computer Language Shootout 
   contributed by Isaac Gouy

To compile:	
   nicec --sourcepath=.. -d=. -a fannkuch.jar fannkuch

To run:
   java -ea -jar fannkuch.jar 10
*/


import ackermann; // reuse toSinglePositiveInt

long fannkuch(int n){
   let perm = new long[n];
   let perm1 = new long[n];
   let count = new long[n];
   let maxPerm = new long[n];      
   var maxFlipsCount = 0l;
   var m = n - 1;

   for(var i=0; i<n; i++) perm1[i] = i;
   var r = n;
   while (true){
      while (r != 1){ count[r-1] = r; r--; }  
      if (! (perm1[0]==0 || perm1[m] == m)){       
         for(var i=0; i<n; i++) perm[i] = perm1[i];         
         var flipsCount = 0;
         long k;
         while ( !((k=perm[0]) == 0) ){
            int k2 = cast( (k+1)>>1 );
            for(var i=0; i<k2; i++){
               let temp = perm[i]; perm[i] = perm[cast(k-i)]; perm[cast(k-i)] = temp;
            }
            flipsCount++;
         }
         
         if (flipsCount > maxFlipsCount){
            maxFlipsCount = flipsCount;
            for(var i=0; i<n; i++) maxPerm[i] = perm1[i];
         }
      }         

      while (true){        
         if (r == n) return maxFlipsCount;
         let perm0 = perm1[0];
         var i = 0;
         while (i < r){
            let j = i + 1;
            perm1[i] = perm1[j];
            i = j;
         }
         perm1[r] = perm0;

         count[r] = count[r] - 1;
         if (count[r] > 0) break;
         r++;
      }
   }
}

void main(String[] args){
   let n = toSinglePositiveInt(args);
   println("Pfannkuchen(" + n + ") = " + fannkuch(n));
}

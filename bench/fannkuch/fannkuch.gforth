\ read NUM from last command line argument
0. argc @ 1- arg >number 2drop drop constant NUM

create f NUM allot
variable max-flips

: rev ( n -- )
  dup f + swap 2/ f + f do
    1-  i c@ over c@  i c! over c!
  loop drop ;

: flips ( -- n )  0
  begin f c@ 1- while 1+  f c@ rev repeat ;

: >f ( <items> -- <items> )
  depth 0 do i pick i f + c! loop ;

: perms ( <items> n -- <items> n )
  dup 1 = if
    drop >f flips max-flips @ max max-flips ! 1
  else  
    dup 0 do
      dup >r 1- recurse roll r>
    loop
  then ;

: items ( n -- <1..n> n ) dup 1 do i swap loop dup ;
: drops ( <n items> n -- ) 0 do drop loop ;

\ run and print result from stack
   0 max-flips !  NUM items perms drops
   ." Pfannkuchen(" NUM 1 u.r ." ) = " max-flips @ 1 u.r cr

bye  \ done!



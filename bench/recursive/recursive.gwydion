module: recursive
use-libraries: common-dylan, io
use-modules: common-dylan, format-out

// ---------------------------------------------------------------------
// The Great Computer Language Shootout
// http://shootout.alioth.debian.org/
//
// Code based on / inspired by existing, relevant Shootout submissions
//
// Contributed by Anthony Borla
// ---------------------------------------------------------------------

define function ack(x :: <integer>, y :: <integer>) => result :: <integer>;
  if (x == 0)
    y + 1;
  elseif (y == 0)
    ack(x - 1, 1);
  else
    ack(x - 1, ack(x, y - 1));
  end if;
end function ack;

// --------------

define function fib(n :: <integer>) => result :: <integer>; 
  if (n < 2)
    1;
  else
    fib(n - 2) + fib(n - 1);
  end if;
end function fib;

define function fibflt(n :: <double-float>) => result :: <double-float>; 
  if (n < 2.0d0)
    1.0d0;
  else
    fibflt(n - 2.0d0) + fibflt(n - 1.0d0);
  end if;
end function fibflt;

// --------------

define function tak(x :: <integer>, y :: <integer>, z :: <integer>) => result :: <integer>;
  if (y < x)
    tak(tak(x - 1, y, z), tak(y - 1, z, x), tak(z - 1, x, y));
  else
    z;
  end if;
end function tak;

define function takflt(x :: <double-float>, y :: <double-float>, z :: <double-float>) => result :: <double-float>;
  if (y < x)
    takflt(takflt(x - 1.0d0, y, z), takflt(y - 1.0d0, z, x), takflt(z - 1.0d0, x, y));
  else
    z;
  end if;
end function takflt;

// --------------------------------

begin
  let n = application-arguments()[0].string-to-integer;

  format-out("Ack(3,%d): %d\n", n, ack(3, n));
  format-out("Fib(%.1f): %.1f\n", 27.0d0 + n, fibflt(27.0d0 + n));

  n := n - 1;
  format-out("Tak(%d,%d,%d): %d\n", n * 3, n * 2, n, tak(n * 3, n * 2, n));

  format-out("Fib(3): %d\n", fib(3));
  format-out("Tak(3.0,2.0,1.0): %.1f\n", takflt(3.0d0, 2.0d0, 1.0d0));
end


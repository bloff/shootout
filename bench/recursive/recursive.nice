/* The Computer Language Shootout 
   http://shootout.alioth.debian.org/
   contributed by Isaac Gouy 
*/

import Include;

void main(String[] args){
   let n = Integer.parseInt(args[0]); 
   var m = n+4;
   println("Ack(3," + m + "): " + ack(3,m)); 
  
   m = n+9; println("Fib(" + m + "): " + fib(m));
   m = n+32; println("Fib(" + m + "): " + fib(m*1.0).toStringWith(1));

   m = n; println("Tak("+m*3+","+m*2+","+m+"): "+ tak(m*3,m*2,m));
   m = n+2; println("Tak("+m*3+","+m*2+","+m+"): "+ tak(m*3.0,m*2.0,m*1.0));
}

int ack(int m,int n) {
   if (m == 0) return n + 1;
   if (n == 0) return ack(m-1, 1);
   return ack(m-1, ack(m, n-1)); 
}

int fib(int n) = n < 2 ? 1 : fib(n-2) + fib(n-1);

int tak (int x, int y, int z) =
   y >= x ? z : tak( tak(x-1,y,z), tak(y-1,z,x), tak(z-1,x,y) ); 

double fib(double n) = n < 2 ? 1 : fib(n-2) + fib(n-1);

double tak (double x, double y, double z) =
   y >= x ? z : tak( tak(x-1,y,z), tak(y-1,z,x), tak(z-1,x,y) ); 

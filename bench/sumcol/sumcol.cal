/** 
 * The Computer Language Benchmarks Game 
 * Based on the CAL Open Quark version
 * Contributed by Magnus Byne.
 */
module Sumcol;

import Cal.Core.Prelude using
    typeConstructor = String, Boolean, Int;
    function = eager, stringToInt, add;
    ;

import Cal.Core.Debug using
    function = show;
    ;

import Cal.Core.String using
    function = length;
    ;

import Cal.Collections.List using
    function = foldLeft1Strict, map, head;
    ;


data foreign unsafe import jvm "java.io.BufferedReader" private JBufferedReader;

data foreign unsafe import jvm "java.io.InputStream" private JInputStream;

data foreign unsafe import jvm "java.io.InputStreamReader"
    private JInputStreamReader;

data foreign unsafe import jvm "java.io.Reader" private JReader;

data foreign unsafe import jvm "java.io.PrintStream" private JPrintStream;

data foreign unsafe import jvm "java.io.FileReader" private JFileReader;

foreign unsafe import jvm "constructor"
    private createBufferedReader :: JReader -> JBufferedReader;

foreign unsafe import jvm "constructor"
    private createInputStreamReaderStream :: JInputStream -> JInputStreamReader;

foreign unsafe import jvm "method readLine"
    private readLine :: JBufferedReader -> String;

foreign unsafe import jvm "static field java.lang.System.out"
    private stdout :: JPrintStream;

foreign unsafe import jvm "static field java.lang.System.in"
    private stdin :: JInputStream;

foreign unsafe import jvm "isNull" private isNull :: String -> Boolean;

foreign unsafe import jvm "method println"
    private printLn :: JPrintStream -> String -> ();

foreign unsafe import jvm "cast"
    jInputStreamReaderToJReader :: JInputStreamReader -> JReader;

main :: [String] -> ();
public main args =
    let
        bufferReader =
            eager
            $ createBufferedReader
                (jInputStreamReaderToJReader
                    (createInputStreamReaderStream stdin)
                )
            ;

        loop :: Int -> Int;
        loop !sum =
            let
                line = eager $ readLine bufferReader;
            in
                if isNull line then
                    sum
                else
                    loop (sum + stringToInt line)
            ;
    in
        printLn stdout (show (loop 0))
    ;


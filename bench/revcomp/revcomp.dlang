/* The Great Computer Language Shootout
   http://shootout.alioth.debian.org/

   Contributed by Dave Fladebo

   compile: dmd -O -inline -release revcomp.d
*/

import std.cstream, std.string;

int main()
{
    char[4096]      bufr;
    BufferedStream  bsi = new BufferedStream(din,bufr.length);
    int             offset;
    char[]          desc,line,data,rc = new char[char.max];

    for(char c = 0; c < char.max; c++) rc[c] = revComp(c);

    foreach(char[] ln; bsi)
    {
        line = ln;
        if(line.length)
        {
            if(line[0] == '>')
            {
                writeRC(desc,data[0..offset],line,rc);
                offset = 0;
            }
            else
            {
                int end = offset + line.length;
                if(data.length < end) data.length = end * 2;
                data[offset..end] = line;
                offset += line.length;
            }
        }
    }
    writeRC(desc,data[0..offset],line,rc);

    return 0;
}

void writeRC(inout char[] desc,char[] data,char[] line,char[] rc)
{
    if(desc.length)
    {
        const int lineLen = 60;
        char[lineLen] lineOut;

        dout.writefln(desc);
        for(int i = data.length - 1, j = 0; i >= 0; i--)
        {
            lineOut[j] = data[i] = rc[data[i]];

            j++;
            if(j == lineLen || i == 0)
            {
                dout.writeLine(lineOut[0..j]);
                j = 0;
            }
        }
    }

    desc = line.dup;
}

char revComp(char c)
{
    c = std.ctype.toupper(c);

    switch(c)
    {
    case 'A': c = 'T'; break;
    case 'B': c = 'V'; break;
    case 'C': c = 'G'; break;
    case 'D': c = 'H'; break;
    case 'G': c = 'C'; break;
    case 'H': c = 'D'; break;
    case 'K': c = 'M'; break;
    case 'M': c = 'K'; break;
    case 'R': c = 'Y'; break;
    case 'T': c = 'A'; break;
    case 'V': c = 'B'; break;
    case 'Y': c = 'R'; break;
    default :          break;
    }

    return c;
}

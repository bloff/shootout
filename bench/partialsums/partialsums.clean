/* The Computer Language Shootout
   http://shootout.alioth.debian.org/
   contributed by Isaac Gouy (Clean novice) */

module partialsums
import StdEnv, LanguageShootout

Start = 
   toStringWith 9 (twoThirds 0.0 n 0.0) +++ "\t(2/3)^k\n" +++
   toStringWith 9 (k05 1.0 n 0.0) +++ "\tk^-0.5\n" +++
   toStringWith 9 (kk1 1.0 n 0.0) +++ "\t1/k(k+1)\n" +++   
   toStringWith 9 (flintHills 1.0 n 0.0) +++ "\tFlint Hills\n" +++
   toStringWith 9 (cooksonHills 1.0 n 0.0) +++ "\tCookson Hills\n" +++
   toStringWith 9 (harmonic 1.0 n 0.0) +++ "\tHarmonic\n" +++
   toStringWith 9 (riemannZeta 1.0 n 0.0) +++ "\tRiemann Zeta\n" +++
   toStringWith 9 (altHarmonic 1.0 n 1.0 0.0) +++ "\tAlternating Harmonic\n" +++
   toStringWith 9 (gregory 1.0 n 1.0 0.0) +++ "\tGregory\n"     
   where n = toReal argi

twoThirds k n sum
   | k < n    = twoThirds(k+1.0) n (sum + ((2.0/3.0)^k))
              = sum

k05 k n sum
   | k <= n   = k05(k+1.0) n (sum + (k^ ~0.5))
              = sum

kk1 k n sum
   | k <= n   = kk1(k+1.0) n (sum + 1.0/(k*(k+1.0)))
              = sum        
  
flintHills k n sum
   | k <= n   = flintHills(k+1.0) n (sum + 1.0/((k^3.0)*((sin k)^2.0)))
              = sum
  
cooksonHills k n sum
   | k <= n   = cooksonHills(k+1.0) n (sum + 1.0/((k^3.0)*((cos k)^2.0)))
              = sum     
      
harmonic k n sum
   | k <= n   = harmonic(k+1.0) n (sum + 1.0/k)
              = sum

riemannZeta k n sum
   | k <= n   = riemannZeta(k+1.0) n (sum + 1.0/(k^2.0))
              = sum 
   
altHarmonic :: !Real !Real !Real !Real -> Real   
altHarmonic k n a sum
   | k <= n   = altHarmonic(k+1.0) n (~a)(sum + a/k)
              = sum
   
gregory :: !Real !Real !Real !Real -> Real   
gregory k n a sum
   | k <= n   = gregory(k+1.0) n (~a)(sum + a/(2.0*k - 1.0))
              = sum

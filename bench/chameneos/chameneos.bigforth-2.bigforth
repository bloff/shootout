\ The Computer Language Shootout
\ http://shootout.alioth.debian.org
\
\ contributed by Ian Osgood
\ modified by Bernd Paysan

\ read NUM from last command line argument
0. argc @ 1- arg >number 2drop drop value NUM

\ 0 is none
1 constant red
2 constant yellow
3 constant blue

: p4-opt-Variable  cell allocate throw Value ;

p4-opt-Variable first
p4-opt-Variable second
Variable meetings

: meet ( me -- other )
  begin pause second @ 0= until
  first @
  if   second ! first @ first off
  else NUM dup 0= if nip exit then 1- to NUM
       first !
       begin pause second @ until
       second @ second off
  then ;

: complement ( c1 c2 -- c3 )
  2dup = if drop else + 6 swap - then ;

Variable creatures

: creature ( color -- )
  1 creatures +!
  64 dup NewTask 1 swap pass
  ( task:  color -- )
  0 swap ( #met color )
  begin  dup meet ?dup
  while  complement swap  1+ swap
  repeat
  drop  meetings +! -1 creatures +! ;

: main
  blue yellow red blue  4 0 do creature loop
  begin pause creatures @ 0= until ;

main  meetings @ 1 u.r cr  bye  \ done!

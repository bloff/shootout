/* The Great Computer Language Shootout
   http://shootout.alioth.debian.org/

   Contributed by Dennis Decker Jensen
*/

size = 10000
arg n .

do n
	list1 = ''
	do i = 1 for size; list1 = list1 i; end
	list2 = copies(list1, 1)

	list3 = ''
	do while list2 \= ''
		list3 = list3 word(list2, 1)
		list2 = delword(list2, 1, 1)
	end

	last = size
	do while list3 \= ''
		list2 = list2 word(list3, last)
		list3 = delword(list3, last, 1)
		last = last - 1
	end
	list2 = strip(list2, 'Both') /* remove superfluous spaces */

	front = 1
	back = size
	do words(list1) % 2
		list1 = subword(list1, 1, front-1) ,
				word(list1, back) ,
				subword(list1, front+1, back-front-1) ,
				word(list1, front) ,
				subword(list1, back+1)
		front = front + 1
		back = back - 1
	end
	list1 = space(list1) /* equal space between elements */

	if word(list1, 1) = size & list1 = list2 then
		count = size
	else do
		count = -1
	end
end

say count 


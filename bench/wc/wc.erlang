%%% -*- mode: erlang -*-
%%% $Id: wc.erlang,v 1.5 2004-09-28 04:02:04 bfulgham Exp $
%%% http://www.bagley.org/~doug/shootout/
%%%
%%% Super-fast implementation by Mark Scandariato
%%% Mega-improvement by Alexey Shchepin <alexey@sevcom.net>

-module(wc).
-export([main/0, main/1]).

-define(BUFSIZE, 8192).

main() -> main(['1']).
main(_Args) ->
    ok = io:setopts([binary]),
    s([], 0, 0, 0).

done(L, W, C) ->
    io:fwrite("~p ~p ~p~n", [L, W, C]),
    erlang:halt(0).

s([], C, L, W) ->
    case io:get_chars('', ?BUFSIZE) of
	eof -> done(L, W, C);
	Bin -> s(binary_to_list(Bin), C, L, W)
    end;
s([Char | Rest], C, L, W) ->
    case Char of
	$\s -> s(Rest, C+1, L,   W);
	$\t -> s(Rest, C+1, L,   W);
	$\n -> s(Rest, C+1, L+1, W);
	_ ->   w(Rest, C+1, L,   W+1)
    end.

w([], C, L, W) ->
    case io:get_chars('', ?BUFSIZE) of
	eof -> done(L, W, C);
	Bin -> w(binary_to_list(Bin), C, L, W)
    end;
w([Char | Rest], C, L, W) ->
    case Char of
	$\s -> s(Rest, C+1, L,   W);
	$\t -> s(Rest, C+1, L,   W);
	$\n -> s(Rest, C+1, L+1, W);
	_ ->   w(Rest, C+1, L,   W)
    end.


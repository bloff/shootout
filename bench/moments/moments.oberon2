(* The Great Computer Language Shootout
   http://shootout.alioth.debian.org

   contributed by Isaac Gouy (Oberon-2 novice)
*)


MODULE Moments;
IMPORT In, Out, LRealMath, LangShootout, SYSTEM;

TYPE
   Reals = POINTER TO ARRAY OF REAL;

VAR
   x: Reals;
   i, n, mid: LONGINT;
   sum, mean, dev, adev, dev2, var, skew, kurt, sdev, median: LONGREAL;


PROCEDURE ReadReals(VAR i: LONGINT; VAR sum: LONGREAL): Reals;
VAR
   x, tmp: Reals;
   xi: REAL;
   size: LONGINT;
BEGIN
   i := 0; sum := 0.0; size := 4096;
   NEW(x, size);
   LOOP
      In.Real(xi); 
      IF ~In.Done() THEN EXIT; END;
      
      sum := sum + xi;    
      IF i = size THEN 
         NEW(tmp, ASH(size,1));
         SYSTEM.MOVE( SYSTEM.ADR(x[0]), SYSTEM.ADR(tmp[0]), size);  
         x := tmp; tmp := NIL;    
      END;               
      x[i] := xi; INC(i); 
   END;
   RETURN x;
END ReadReals;


BEGIN
   x := ReadReals(n,sum);
   mean := sum / n;
   FOR i := 0 TO n-1 DO
      dev := x[i] - mean;
      adev := adev + ABS(dev);
      dev2 := dev * dev;
      var := var + dev2;
      skew := skew + dev2 * dev;
      kurt := kurt + dev2 * dev2;
   END;
   adev := adev / n;
   var := var / (n - 1);
   sdev := LRealMath.sqrt(var);
   IF var # 0.0 THEN
      skew := skew / (n * var * sdev);
      kurt := kurt / (n * var * var) - 3.0;
   END;

   LangShootout.Quicksort(n, x^);
   mid := (n DIV 2) - 1;
   IF ~ODD(mid) THEN median := x[mid];
                ELSE median := (x[mid] + x[mid+1]) / 2; END; 

   Out.String("n:                  "); Out.Int(n,0); Out.Ln;
   Out.String("median:             "); Out.LongRealFix(median,0,6); Out.Ln;
   Out.String("mean:               "); Out.LongRealFix(mean,0,6); Out.Ln;
   Out.String("average_deviation:  "); Out.LongRealFix(adev,0,6); Out.Ln;
   Out.String("standard_deviation: "); Out.LongRealFix(sdev,0,6); Out.Ln;
   Out.String("variance:           "); Out.LongRealFix(var,0,6); Out.Ln;
   Out.String("skew:               "); Out.LongRealFix(skew,0,6); Out.Ln;
   Out.String("kurtosis:           "); Out.LongRealFix(kurt,0,6); Out.Ln;
END Moments.



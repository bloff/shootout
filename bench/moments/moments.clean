/* The Great Computer Language Shootout 
   contributed by Isaac Gouy (Clean novice)

   http://shootout.alioth.debian.org/

To compile:	
   cleanIDE.exe --batch-force-build "e:\shoot\contrib\clean\moments.prj"

To run:
   moments.exe -con < input.txt > output.txt
*/


module moments
import StdEnv, StdOverloadedList, LanguageShootout, Heapsort

Start world  
   # (console, world) = stdio world
   # (a,suma) = sumNumbers console 0.0 [#!]
   # (median,n,a) = median a   
   # mean = suma / toReal n      
   # (adev,var,skew,kurt) = loop a mean 0.0 0.0 0.0 0.0 (n-1)
   = resultstring (toReal n) mean median adev var skew kurt    

   where         
   sumNumbers :: !*File !Real .[#Real!] -> *(*{#Real},Real)   
   sumNumbers f suma a
      #! (ok,r,f) = freadr f
      | not ok = (toArray a, suma)
      = sumNumbers f (suma+r) [# r:a !]
      
   toArray :: [#Real!] -> *{#Real}
   toArray a = {x \\ x <|- a}           
                                       
      
   // Just use heapsort rather than Wirth's k select       
   median a
      # a = heapsort a  
      # (n,a) = usize a
      # m = middle a ((n/2)-1) (n/2) n
      = (m,n,a)
      
      where 
      middle a=:{[m0]=am0,[m]=am} m0 m n
         | isOdd n = am
                   = (am0 + am) / 2.0
                   
                   
   loop a=:{[i]=ai} mean adev var skew kurt i
      | i<0   = (adev,var,skew,kurt)
      = loop a mean  (adev + abs dev)(var + dev*dev)
         (skew + dev*dev*dev)(kurt + dev*dev*dev*dev)(i-1)
      where dev = ai - mean      
                            

   resultstring n mean median ad v s k = 
      "n:                  " +++ (toString n) +++ "\n" +++ 
      "median:             " +++ (toStringWith 6 median) +++ "\n" +++ 
      "mean:               " +++ (toStringWith 6 mean) +++ "\n" +++            
      "average_deviation:  " +++ (toStringWith 6 adev) +++ "\n" +++ 
      "standard_deviation: " +++ (toStringWith 6 sdev) +++ "\n" +++ 
      "variance:           " +++ (toStringWith 6 var) +++ "\n" +++ 
      "skew:               " +++ (toStringWith 6 skew) +++ "\n" +++ 
      "kurtosis:           " +++ (toStringWith 6 kurt) +++ "\n"     
      
      where                                                 
      adev = ad / n      
      var = v / (n-1.0)
      sdev = sqrt var
      skew = s / (n*var*sdev)
      kurt = k / (n*var*var)-3.0                                                 
